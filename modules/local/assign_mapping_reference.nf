process ASSIGN_MAPPING_REFERENCE {
    tag "$group_meta.id"
    label 'process_single'

    conda "conda-forge::r-base=4.2.1"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/r-base:4.2.1' :
        'biocontainers/r-base:4.2.1' }"

    input:
    tuple val(group_meta), path(ani_matrix), path(samp_ref_pairs)
    val min_ref_ani

    output:
    tuple val(group_meta), path("${prefix}_reassigned.csv"), emit: samp_ref_pairs
    path "versions.yml"                                    , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    prefix = task.ext.prefix ?: "${group_meta.id}"
    """
    assign_mapping_reference.R ${ani_matrix} ${samp_ref_pairs} ${prefix}_reassigned.csv ${min_ref_ani}

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        r-base: \$(echo \$(R --version 2>&1) | sed 's/^.*R version //; s/ .*\$//')
    END_VERSIONS
    """
}
